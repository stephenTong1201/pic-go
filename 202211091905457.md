### IIC串行EEPROM（读写控制器）

**IIC CMOS串行EEPROM**是一种采用CMOS工艺制成的**串行可用电擦除可编程随机读写存储器**。串行EEPROM有两种写入方式，一种是字节写入，还有一种页写入，分别允许在一个写周期内同时对一个字节到一页的若干字节进行编程写入。

**IIC总线要点：**双向二线制串行总线协议（Inter Integrated Circuit）,<u>只有在总线处于"非忙"状态时，数据传输才能开始。在数据传输期间，只要时钟线为高电平，数据线都必须保持稳定，否则数据线上的任何变化都被当做“启动''或"停止"信号</u>。

![image-20221031110049657](C:\Users\admin\AppData\Roaming\Typora\typora-user-images\image-20221031110049657.png)

A：总线非忙状态，sda与scl保持高电平

B：启动数据传输，scl为高且sda下降沿，认为是启动信号

C：停止数据传输，scl为高且sda上升沿，认为是停止信号->A

D：数据有效，scl为高时数据线稳定，scl为低时sda才发生数据改变，每个数据占用一个时钟脉冲。由启动信号开始，结束信号结束。

**应答信号**：每个正在接收数据的EEPROM在接收到一个字节数据后，通常需要发出一个应答信号。而每个正在发送数据的EEPROM在发出一个字节的数据后，通常需要接收一个应答信号。EEPROM读写控制器必须产生一个与这个应答位相联系的额外时钟脉冲。在EEPROM的读操作中，EEPROM读写控制器对EEPROM完成的最后一个字节不产生应答位，但是应该给EEPROM一个结束信号。

**IIC串行EEPROM的读写操作：**

**写操作**：读写控制器发送”启动“信号后，紧跟着送4位IIC总线器件**特征编码1010**和3位EEPROM**芯片/页地址XXX**，以及**读写状态（R1/W0=0）**到总线上，一共1字节（8bit），紧接EEPROM发送应答位。读写控制器将跟着发送一个字节的**EEPROM存储单元地址**。EEPROM在接收到存储单元地址后，又一次产生**应答位**，使读写控制器发送数据字节，并把数据写入被寻址的存储单元。EEPROM再一次发出应答信号，读写控制器收到此应答信号后便产生停止信号。

![image-20221031114521968](C:\Users\admin\AppData\Roaming\Typora\typora-user-images\image-20221031114521968.png)

**读操作**：读写器发送一个启动信号和控制字节（1010+芯片/页地址XXX+写控制位0），先进行写操作，在EEPROM应答后，**通过写操作设置EEPROM存储单元地址**。读写器再**重新发送一个启动信号和控制字节**（1010XXX**1**），EEPROM收到后发出**应答信号**，然后要寻址存储单元的数据就从sda线上输出。最后有个为高的**非应答位**。

![image-20221101102148348](C:\Users\admin\AppData\Roaming\Typora\typora-user-images\image-20221101102148348.png)

**Verilog设计**：

![image-20221101102221498](C:\Users\admin\AppData\Roaming\Typora\typora-user-images\image-20221101102221498.png)

EEPROM不需要进行可综合风格的设计，主要设计读写控制器，EEPROM只需要设计一个行为模型。scl为串行时钟端，这个信号对输入和输出数据同步，写入串行EEPROM的数据利用其上升沿同步，输出数据用其下降沿同步；sda为串行数据输入/输出总线端。

EEPROM部分verilog代码：

```verilog
//behavioral design,not RTL-synthethis design
module EEPROM(
input scl,
inout sda
    );
    reg[7:0] memory[2047:0];//数据大小8bit，存储空间2048，共8页，每页可存储256字节
    reg[10:0] address;//二进制数据地址2048，{二进制页数3位，二进制页内地址8位}
    reg[7:0] inbuf;//写入操作的数据寄存器
    reg[7:0] outbuf;//输出操作时的数据寄存器
    reg[7:0] addrbuf;//数据地址寄存器，页内地址，每页256
    reg[7:0] ctrlbuf;//控制信号寄存器
    reg[1:0] state;//工作状态
    reg outflag;//sda控制数据开始输出的控制信号
    reg[7:0] memorybuf;//写入memory的数据寄存器
    
integer i;
//read=1,write=0
parameter r7=8'b10101111,r6=8'b10101101,r5=8'b10101011,r4=8'b10101001,
            r3=8'b10100111,r2=8'b10100101,r1=8'b10100011,r0=8'b10100001,
            w7=8'b10101110,w6=8'b10101100,w5=8'b10101010,w4=8'b10101000,
            w3=8'b10100110,w2=8'b10100100,w1=8'b10100010,w0=8'b10100000;
     
    
assign sda=outflag?outbuf[7]:1'bz;//输出控制信号控制串行输出

    
//初始化
initial
  begin
    addrbuf=0;
    ctrlbuf=0;
    outflag=0;
    outbuf=0;
    inbuf=0;
    state=0;
    memorybuf=0;
    addrbuf=0;
    address=0;
    for(i=0;i<=2047;i=i+1)
      memory[i]=0;//初始化空EEPROM
  end

    
always@(negedge sda)//scl为高时，sda下降沿为启动信号
  if(scl)
    begin
      state=state+1;//启动信号触发的状态转移
      if(state==2'b11) disable write_to_eeprom;//写操作结束出现启动信号，停止写操作，要进入读操作了
    end

    
always@(posedge sda)//主状态机，启动需要下降沿已经在低位，所以输入控制字节1010XXX0/1肯定有上升沿
  if(scl)
    stop_W_R;//收到停止信号，停止状态
  else//在scl为低时出现sda上升沿，进入状态判断
    begin
      case(state)
        2'b01://启动后第一个状态
             begin
             read_in;//读控制字节和存储单元地址字节，进行下面判断
                       if(ctrlbuf==w7||ctrlbuf==w6||ctrlbuf==w5||ctrlbuf==w4||ctrlbuf==w3||ctrlbuf==w2||ctrlbuf==w1||ctrlbuf==w0)
               begin
                 state=2'b10;//下一个状态，因为不管是读还是写操作，都需要以写地址开始
                 write_to_eeprom;//如果控制字节为写操作，则进行写任务，写任务随停止信号停止
               end
             else
               state=2'b00;//如果控制信号不是写操作，说明出现问题，自动停止，返回启动前的状态
             end
        2'b11://如果再次出现启动信号，state会加1，所以在写操作后结束再启动，就是读操作
             read_from_eeprom;//读操作，不进行状态变化，等待停止信号
        default:
             state=2'b00;
      endcase
    end
  
    
  task stop_W_R;//返回初始状态
    begin
      state=2'b00;
      addrbuf=0;
      ctrlbuf=0;
      outflag=0;
      outbuf=0;
    end
  endtask
  
    
  task read_in;//读取控制字节和存储单元地址字节
    begin
      shift_in(ctrlbuf);
      shift_in(addrbuf);
    end
  endtask
  
    
  task write_to_eeprom;//写操作，将指定的数据写入指定地址的memory中
    begin
        shift_in(memorybuf);//写入sda串行数据一个字节到memorybuf
      address={ctrlbuf[3:1],addrbuf};
      memory[address]=memorybuf;//将前面存入memorybuf的sda数据写入memory指定address
      $display("eeprom---memory[%d]=%b",address,memory[address]);
      state=2'b00;
    end
  endtask
 
    
  task read_from_eeprom;
    begin
        shift_in(ctrlbuf);//写入sda串行数据一个字节到控制字节ctrlbuf
      if(ctrlbuf==r7||ctrlbuf==r6||ctrlbuf==r5||ctrlbuf==r4||ctrlbuf==r3||ctrlbuf==r2||ctrlbuf==r1||ctrlbuf==r0)//检查到读控制位
        begin
          address={ctrlbuf[3:1],addrbuf};//定位页+页内地址->address
          shift_out;//串行输出outbuf
          state=2'b00;
        end
      end
    endtask
    
    
    task shift_in;//写入一个字节到指定寄存器，在时钟上升沿寄存sda数据，在写入完毕后下降沿发送应答信号0
      output[7:0] inbuf;//返回inbuf到主always块
      begin
        @(posedge scl) inbuf[7]=sda;//上升沿写入
        @(posedge scl) inbuf[6]=sda;
        @(posedge scl) inbuf[5]=sda;
        @(posedge scl) inbuf[4]=sda;
        @(posedge scl) inbuf[3]=sda;
        @(posedge scl) inbuf[2]=sda;
        @(posedge scl) inbuf[1]=sda;
        @(posedge scl) inbuf[0]=sda;
      //每次上升沿都写入一个数据位到inbuf  
        @(negedge scl)
          begin
            #100;
            outflag=1;
            outbuf=0;//每次写入字节结束的应答信号
          end
          
        @(negedge scl)
          #100 outflag=0;
          
      end
    endtask
    
    
    task shift_out;//读一个字节，outflag置1，允许sda输出，在时钟下降沿时将outbuf按100ns频率左移位
      begin
        outflag=1;
        for(i=6;i>=0;i=i-1)
          begin
              @(negedge scl)//下降沿输出
              #100;
              outbuf=outbuf<<1;//scl下降沿同步串行输出
          end
        @(negedge scl) #100 outbuf[7]=1;//读操作结束后的非应答位？
        @(negedge scl) #100 outflag=0;
      end
    endtask//每100输出一个数据到sda总线，最后附带读操作特有的非应答位（高电平）
endmodule
```

 EEPROM读写器可综合模型：接收来自于信号源模型产生的读信号、写信号、并行地址信号和并行数据信号，并把它们转换为相应的串行信号发送到串行EEPROM行为模型。还发送应答信号ack到信号源模型，以便让信号源来调节发送或接收数据的速度，以配合读写数据。由两部分组成，**开关组合电路和控制时序电路**。开关组合电路在控制时序电路的控制下，按照要求有节奏地打开或闭合，这样sda可以按IIC数据总线的格式输出或输入。
![image-20221101162000549](C:\Users\admin\AppData\Roaming\Typora\typora-user-images\image-20221101162000549.png)

嵌套状态机（task里带状态机）

读写控制模块verilog代码：

```verilog
module WR(
input[10:0] address,
input clk,reset,RD,WR,
inout[7:0] data,
output reg scl,
inout sda,
output reg ack
    );
    reg link_read,link_write,link_head,link_stop,link_sda;//开关寄存器
    reg[7:0] writebuf,readbuf;//读写缓冲区
    reg[1:0] headbuf,stopbuf;//启动/停止信号，记录上升下降沿
    reg wflag,rflag;//读写模式标志
    reg[2:0] head_state,stop_state;//启动和停止分3步
    reg FF;//控制位读写标志寄存器?
    reg[8:0] out_state;
    reg[8:0] in_state;
    reg[10:0] main_state;
    
wire sda1,sda2,sda3,sda4;

parameter
          idle       =11'b00000000001,//主状态
          ready      =11'b00000000010,
          write_start=11'b00000000100,
          ctrl_write =11'b00000001000,
          addr_write =11'b00000010000,
          data_write =11'b00000100000,
          read_start =11'b00001000000,
          ctrl_read  =11'b00010000000,
          data_read  =11'b00100000000,
          stop       =11'b01000000000,
          ackn       =11'b10000000000,//stop时会将ack信号置1，这个状态用于将其回0
          
          out_bit7=9'b000000001,//写入task状态
          out_bit6=9'b000000010,
          out_bit5=9'b000000100,
          out_bit4=9'b000001000,
          out_bit3=9'b000010000,
          out_bit2=9'b000100000,
          out_bit1=9'b001000000,
          out_bit0=9'b010000000,
          out_end =9'b100000000,
          
          in_bit7=9'b000000001,//读出task状态
          in_bit6=9'b000000010,
          in_bit5=9'b000000100,
          in_bit4=9'b000001000,
          in_bit3=9'b000010000,
          in_bit2=9'b000100000,
          in_bit1=9'b001000000,
          in_bit0=9'b010000000,
          in_end =9'b100000000,
          
          head_begin=3'b001,//启动信号发生task状态
          head_bit  =3'b010,
          head_end  =3'b100,
          
          stop_begin=3'b001,//停止信号发生task状态
          stop_bit  =3'b010,
          stop_end  =3'b100;

    
//开关组合逻辑电路
assign sda1=link_head? headbuf[1]:1'b0;
assign sda2=link_write? writebuf[7]:1'b0;
assign sda3=link_stop? stopbuf[1]:1'b0;//寄存器都输出最高位，靠时序移位来连续输出
assign sda4=(sda1|sda2|sda3);//有哪个输出哪个，靠开关决定
assign sda=link_sda? sda4:1'bz;//最后到总线的开关
assign data=link_read? readbuf:8'hzz;//输出读操作的结果

    
//控制时序电路
always@(negedge clk)
  if(reset)
    scl<=0;
  else scl<=~scl;//生成二分频串行时钟scl

task shift_head;//启动信号传至总线sda
  begin
    case(head_state)
      head_begin://headbuf==2'b01
          if(!scl)
            begin
              link_write<=0;
              link_sda<=1;//sda<=0
              link_head<=1;//打开head开关，将总线sda与headbuf相连
              head_state<=head_bit;
            end
          else
            head_state<=head_begin;
      head_bit:
          if(scl)
            begin
              FF<=1;
              headbuf<=headbuf<<1;//sda<=1
              head_state<=head_end;
            end
          else
            head_state<=head_bit;
      head_end:
          if(!scl)
            begin
              link_head<=0;
              link_write<=1;//发送完启动信号后立马开始写控制字节操作
            end
          else
            head_state<=head_end;
    endcase
  end
endtask


task shift_stop;
  begin
    case(stop_state)
      stop_begin:
          if(!scl)
            begin
              link_write<=0;
              link_stop<=1;
              link_sda<=1;
              stop_state<=stop_bit;
            end
          else
            stop_state<=stop_begin;
      stop_bit:
          if(scl)
            begin
              stopbuf<=stopbuf<<1;
              stop_state<=stop_end;
            end
          else
            stop_state<=stop_bit;
      stop_end:
          if(!scl)
            begin
              link_sda<=0;
              link_head<=0;
              link_stop<=0;
              FF<=1;//为啥停止信号的FF在stop_end状态置1，启动信号FF在head_bit状态置1？
            end
          else
            stop_state<=stop_end;
    endcase
  end
endtask


task shift8in;//读操作从EEPROM返回的sda总线数据，存放至readbuf，最后打开read开关写入data
  begin
    case(in_state)
      in_bit7:
          if(scl)//scl为低时sda数据变化，为高时便写入WR控制器的读寄存器，错开设计
            begin
              readbuf[7]<=sda;
              in_state<=in_bit6;
            end
          else
            in_state<=in_bit7;
      in_bit6:
          if(scl)
            begin
              readbuf[6]<=sda;
              in_state<=in_bit5;
            end
          else
            in_state<=in_bit6;
      in_bit5:
          if(scl)
            begin
              readbuf[5]<=sda;
              in_state<=in_bit4;
            end
          else in_state<=in_bit5;
      in_bit4:
          if(scl)
            begin
              readbuf[4]<=sda;
              in_state<=in_bit3;
            end
          else in_state<=in_bit4;
      in_bit3:
          if(scl)
            begin
              readbuf[3]<=sda;
              in_state<=in_bit2;
            end
          else in_state<=in_bit3;
      in_bit2:
          if(scl)
            begin
              readbuf[2]<=sda;
              in_state<=in_bit1;
            end
          else in_state<=in_bit2;
      in_bit1:
          if(scl)
            begin
              readbuf[1]<=sda;
              in_state<=in_bit0;
            end
          else in_state<=in_bit1;
      in_bit0:
          if(scl)
            begin
              readbuf[0]<=sda;
              in_state<=in_end;
            end
          else in_state<=in_bit0;
      in_end:
          if(scl)
            begin
              link_read<=1;
              FF<=1;//读出数据完毕有FF指示信号，具体是啥呢？
              //应该是每个task操作完成都会置1，再在主状态机判断FF是否为1，如果还是0则还没进行过这个操作，需要进行task
              in_state<=in_bit7;
            end
          else
            in_state<=in_end;
      default:
          begin
            link_read<=0;
            in_state<=in_bit7;
          end
    endcase
  end
endtask


task shift8out;//写操作，将信号源的data逐位写入sda总线
  begin
    case(out_state)
      out_bit7:
          if(!scl)
            begin
              link_sda<=1;
              link_write<=1;//最高位直接打开开关写入
              out_state<=out_bit6;
            end
          else out_state<=out_bit7;
      out_bit6:
          if(!scl)
            begin
              out_state<=out_bit5;
              writebuf<=writebuf<<1;//下一时钟只需左移位，便可依次输出
            end
          else out_state<=out_bit6;
      out_bit5:
          if(!scl)
            begin
              out_state<=out_bit4;
              writebuf<=writebuf<<1;
            end
          else out_state<=out_bit5;
      out_bit4:
          if(!scl)
            begin
              out_state<=out_bit3;
              writebuf<=writebuf<<1;
            end
          else out_state<=out_bit4;
       out_bit3:
           if(!scl)
             begin
               out_state<=out_bit2;
               writebuf<=writebuf<<1;
             end
           else out_state<=out_bit3;
        out_bit2:
            if(!scl)
              begin
                out_state<=out_bit1;
                writebuf<=writebuf<<1;
              end
            else out_state<=out_bit2;
         out_bit1:
             if(!scl)
               begin
                 out_state<=out_bit0;
                 writebuf<=writebuf<<1;
               end
             else out_state<=out_bit1;
          out_bit0:
              if(!scl)
                begin
                  out_state<=out_end;
                  writebuf<=writebuf<<1;
                end
              else out_state<=out_bit0;
          out_end:
              if(!scl)
                begin
                  link_sda<=0;
                  link_write<=0;
                  FF<=1;//写入完毕也有指示信号
                end
              else out_state<=out_end;
        endcase
      end
    endtask
    
    
always@(posedge clk)//主状态机
  if(reset)
    begin
      link_read<=0;
      link_write<=0;
      link_head<=0;
      link_stop<=0;
      link_sda<=0;
      ack<=0;
      rflag<=0;
      wflag<=0;
      FF<=0;
      main_state<=idle;
    end
  else
    begin
      case(main_state)
        idle://非忙状态，开关全断，并且接收到信号源的读写指示
            begin
              link_read<=0;
              link_write<=0;
              link_head<=0;
              link_stop<=0;
              link_sda<=0;
              if(WR)//信号源要求写操作
                begin
                  wflag<=1;
                  main_state<=ready;
                end
              else if(RD)//信号源要求读操作
                begin
                  rflag<=1;
                  main_state<=ready;
                end
              else
                begin
                  wflag<=0;
                  rflag<=0;
                  main_state<=idle;
                end
            end
            
        ready://收到读写指示，启动信号发出
            begin
              link_read<=0;
              link_write<=0;
              link_head<=1;//启动信号开关
              link_stop<=0;
              link_sda<=1;//sda总线有启动信号输出，连接
              headbuf<=2'b10;//下降沿启动信号初始化
              stopbuf<=2'b01;//上升沿，只是对停止信号初始化
              head_state<=head_begin;//初始化启动信号状态机状态
              FF<=0;
              ack<=0;
              main_state<=write_start;
            end
        
        write_start:
            if(FF==0)//还没有进行启动信号输出task
              shift_head;//发出启动信号，不发生状态转移，直到FF=1，进行else内操作
            else//启动信号发送完毕
              begin
                writebuf<={1'b1,1'b0,1'b1,1'b0,address[10:8],1'b0};//控制字节写入buf
                link_head<=0;//启动信号开关断开
                //link_write<=1;//在shift_head里已经开启了write开关，准备写入控制字
                FF<=0;//进行完启动信号输出，归零FF
                out_state<=out_bit7;//初始化控制字节写入
                main_state<=ctrl_write;
              end
        ctrl_write:
            if(FF==0)
              shift8out;//进行控制字节输出到sda
            else
              begin
                out_state<=out_bit7;//准备下一个写操作，地址字节写入
                writebuf<=address[7:0];//页内地址字节存入buf
                FF<=0;
                main_state<=addr_write;
              end
        addr_write:
            if(FF==0)
              shift8out;//进行地址字节输出
            else
              begin
                FF<=0;
                if(wflag)//如果信号源要求是写操作
                  begin
                    out_state<=out_bit7;//继续准备写入数据字节
                    writebuf<=data;//把要写入的数据存至buf
                    main_state<=data_write;//进入写数据状态
                  end
                else if(rflag)//如果要求读操作
                  begin//即需要重新发一个启动信号
                    headbuf<=2'b10;//重置已经移位为全零的headbuf，设置为下降沿
                    head_state<=head_begin;
                    main_state<=read_start;
                    link_head<=1;
                  end
              end
          data_write:
              if(FF==0)
                shift8out;//写入数据字节
              else
                begin
                  stop_state<=stop_begin;//准备发出停止信号
                  link_write<=0;
                  FF<=0;
                  main_state<=stop;
                end
          read_start:
              if(FF==0)
                shift_head;//发送启动信号
              else
                begin
                  writebuf<={1'b1,1'b0,1'b1,1'b0,address[10:8],1'b1};//准备写入控制字节
                  link_head<=0;
                  FF<=0;
                  main_state<=ctrl_read;
                end
          ctrl_read:
              if(FF==0)
                shift8out;//写入控制字节
              else
                begin
                  link_write<=0;
                  link_sda<=0;
                  FF<=0;
                  in_state<=in_bit7;//准备读取sda返回的数据
                  main_state<=data_read;
                end
          data_read:
              if(FF==0)
                shift8in;//读取sda返回数据至readbuf
              else
                begin
                  link_stop<=1;
                  link_sda<=1;//准备发出停止信号，打开总线
                  stop_state<=stop_begin;
                  FF<=0;
                  main_state<=stop;
                end
          stop:
              if(FF==0)
                shift_stop;
              else
                begin
                  ack<=1;//请求下一次读写操作
                  FF<=0;
                  main_state<=ackn;
                end
          ackn://读写请求归零操作
              begin
                ack<=0;
                wflag<=0;
                rflag<=0;
                main_state<=idle;
              end
          default:
              main_state<=idle;
        endcase
      end
    endmodule
```

信号源能产生相应的读写信号、并行地址信号和并行数据信号，并且能接收串行EEPROM读写控制器的应答信号ack，以此调节发送和接收的速度。写操作时输入的地址信号和数据信号数据通过系统命令$readmemh从addr.dat和data.dat文件中取得，而在这俩文件中可以存放任意数据。读操作时从EEPROM读出的数据存入文件eeprom.dat。

信号源部分verilog代码：

```verilog
module signal(
input ack,
inout[7:0] data,
output reg clk,reset,
output reg[10:0] address,
output reg RD,WR
    );
    reg W_R;
    reg[7:0] writebuf;
    reg[10:0] addrbuf[0:255];
    reg[7:0] databuf[0:255];
    reg[7:0] rom[1:2047];
    integer i,j;
    integer OUTFILE;
    parameter test_number=50;
    
assign data=W_R?8'bz:writebuf;//W_R为0才表示写操作，data需要有写入值

always #100 clk=~clk;

initial
  begin
    reset=1;
    i=0;
    j=0;
    W_R=0;
    clk=0;
    RD=0;
    WR=0;
    #1000;
    reset=0;
    
    repeat(test_number)
      begin
        #1000;
        WR=1;
        #200;
        WR=0;
        @(posedge ack);
      end
    #2000;
    W_R=1;
    repeat(test_number)
      begin
        #1000;
        RD=1;
        #200;
        RD=0;
        @(posedge ack);
      end
        //设置读写信号：WR：1000 WR1 200 WR0 1000 WR1 200 WR0 ......*50
//2000          RD:1000 RD1 200 RD0 1000 RD1 200 RD0 ......*50   
    end
        
        
  initial
    begin
        OUTFILE=$fopen("./eeprom.dat");//暂时为空
        $readmemh("./addr.dat",addrbuf);//读文件地址数据
        $readmemh("./data.dat",databuf);//读文件data数据
    end
    
  initial//写操作
    begin
      $display("writing");
      #400;
      for(i=0;i<=test_number;i=i+1)
        begin
          address=addrbuf[i];//文件中读取的地址作为addresss信号送至读写器
          writebuf=databuf[i];//文件中读取的写数据作为data信号送至读写器
            $fdisplay(OUTFILE,"@%0h  %0h",address,writebuf);//将输出地址和数据记录在eeprom.dat文件，address位置存对应的data
          @(posedge ack);//直到下一次请求再i++，进行下一次记录
        end
    end
  
  initial @(posedge W_R)//读操作,切换到读模式，检验送出地址去EEPROM读数据准不准确
    begin
      address=addrbuf[0];
      $fclose(OUTFILE);
      $readmemh("./eeprom.dat",rom);//把eeprom.dat文件中的数据读到rom中
      $display("reading");
      for(j=0;j<=test_number;j=j+1)
        begin
          address=addrbuf[j];//送出address，等待ack信号上升沿，代表读写器返回读出的数据
          @(posedge ack)
            if(data==rom[address])//比对读到的信号跟上面写操作写进去的对不对应
                $display("data %b==rom[%0h]--read right",data,address);
            else
                $display("data %b!=rom[%0h]--read wrong",data,address);
        end
    end
      
  endmodule
```



顶层模块verilog代码：

```verilog
`include "./signal.v"
`include "./WR.v"
`include "./EEPROM.v"

module top(
);
wire reset;
wire clk;
wire RD,WR;
wire ack;
wire[10:0] address;
wire[7:0] data;
wire scl,sda;

parameter test_number=123;

initial #(200*180*test_number) $stop;//???为啥这样设定数据???

signal #(test_number) signal(
                             .reset(reset),
                             .clk(clk),
                             .RD(RD),
                             .WR(WR),
                             .address(address),
                             .ack(ack),
                             .data(data));
WR eeprom_wr(
             .reset(reset),
             .clk(clk),
             .sda(sda),
             .scl(scl),
             .data(data),
             .address(address),
             .WR(WR),
             .RD(RD),
             .ack(ack));
             
EEPROM eeprom(
              .scl(scl),
              .sda(sda)
              );
              
endmodule
```

addr.dat与data.dat文件利用16进制每行写一个数据



**IIC总结：**

- **为啥这样设计，状态机，task嵌套结构：**

  因为每一步，如启动信号的发生、读字节、写字节、停止信号的的发生等操作都有几个状态转移，是一个连续的时序过程，可以写进一个含有状态机的task，且在主状态机中每个状态都需要给下个状态用到的task初始化状态与寄存器。

- **时序为啥这样设置，怎样设置的时延：**

  <u>时序分析？</u>

- **如何仿真的：**

  编写dat数据文件，利用系统函数读写文件，利用一个空文件存储写操作结果，再进行读操作，通过比对数据是否一致来验证读写正确性。**<u>如何才能实现这样的仿真？</u>**

- 如何设计整体架构的：

  主要编写**读写控制模块**（IIC通信接口），EEPROM根据读写控制的操作相应的反应来编写一个**行为模型**，信号源编写读写操作发起信号的时序，因为读写控制模块主要通过WR、RD信号来激发主状态机的启动。

  读写控制模块使用组合逻辑电路和时序电路的结合，组合逻辑主要通过一些**开关来控制连续赋值**，开关的状态主要在时序电路中变换。
